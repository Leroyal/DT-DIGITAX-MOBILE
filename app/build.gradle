plugins {
    id 'com.android.application'
    id 'org.jetbrains.dokka-android'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

group = 'com.digitaltaxusa.digitax'

android {
    compileSdkVersion 30

    // construct version name
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0
    def code = 1
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
        versionCode code
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        // test runner
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            buildConfigField "String", "ENVIRONMENT", "\"test\""
            buildConfigField "boolean", "DEBUG_MODE", "true"
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]
            // debuggable
            debuggable true
        }
        release {
            buildConfigField "String", "ENVIRONMENT", "\"production\""
            buildConfigField "boolean", "DEBUG_MODE", "false"
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$projectDir/docs/kdoc"
    }

    lintOptions {
        // if true, stop the gradle build if errors are found
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        // for view binding:
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // google
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // support
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'

    // lifecycle extensions
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'

    // Room components
    def roomVersion = '2.3.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion" // for Kotlin use kapt instead of annotationProcessor

    // material
    implementation 'com.google.android.material:material:1.4.0'

    // biometrics
    implementation 'androidx.biometric:biometric:1.1.0'

    // DT (DigiTax) application framework
    implementation project(path: ':DTApplicataionFramework')
}
